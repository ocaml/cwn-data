<?xml version="1.0" encoding="utf-8"?>
<html>
  <head>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
    <title>Caml Weekly News</title>
  </head>
  <body>
    <a href="2004.01.06.html">Previous week</a> <a href="index.html">Up</a> <a href="2004.01.20.html">Next week</a><p>Hello</p><p>Here is the latest Caml Weekly News, for the week of 06 to 13 January, 2004.</p><ol>
      <li>
        <a href="#1">Encoding existential types without modules</a>
      </li>
      <li>
        <a href="#2">Adding an ocaml interpreter to my C program</a>
      </li>
    </ol><a name="1" /><hr /><h3>Encoding existential types without modules</h3><i>Daniel Bünzli said:</i><br /><pre>
I think this may be usefull to others (e.g. to port some clever haskell
code). Below, I give two examples that show how to encode existential
types in ocaml without using modules. This is done by adapting to ocaml
the encoding given by Pierce in [1]. It uses polymorphic record fields.

The examples are a little bit silly but their aim is to show the
concept of the encoding.
The first example is a counter abstract datatype. The second one is a
datatype that can hold a list of composable function, that is a type
that expresses something like

 type ('a, 'b) funlist = Nil of ('a -&gt;'b) | Cons of exists 'c. ('a -&gt;
'c) * ('c,'b) funlist

I usually need three types to encode an existential type. Does anybody
see a simpler way of doing that ?

Daniel

[1] Benjamin C. Pierce, Types and Programming Languages, section 24.3


--- Abstract counter datatype

(* The type expressed by the three types below is :
   type packed_counter =
      exists 'x. { create : 'x; get : ('x -&gt; int); inc : ('x -&gt; 'x)}
*)
type 'x counter = { create : 'x;  get : ('x -&gt; int); inc : ('x -&gt; 'x) }
type 't counter_scope = { bind_counter : 'x. 'x counter -&gt; 't }
type packed_counter = { open_counter : 't. 't counter_scope -&gt; 't }

(* Creating a package from a counter implementation *)
let pack_counter impl = { open_counter = fun scope -&gt;
scope.bind_counter impl }

(* Using a package with a scoped expression *)
let with_packed_counter p e = p.open_counter e

(* Two different implementations of the counter *)
let int_impl = { create = 1 ; get = (function i -&gt; i) ; inc = (fun i -&gt;
i+1) }
let float_impl = { create = 1.0; get = (function i -&gt; (int_of_float i))
;
                   inc = (fun i -&gt; i +. 1.0) }

let counter = pack_counter int_impl
let counter' = pack_counter float_impl

(* An expression using an abstract counter *)
let expr =
  { bind_counter = fun counter -&gt; (* counter is bound to the &lt;&lt;
interface &gt;&gt; *)
    (counter.get (counter.inc counter.create)) }

let result = with_packed_counter counter expr
let result' = with_packed_counter counter' expr

(*
   This doesn't type, the counter type is abstract !
   let expr =
   { bind_counter = fun counter -&gt;
     (counter.get (counter.inc (counter.get counter.create))) }
*)

(*
   This doesn't type, the abstract type tries to escape its scope !
   let expr = { bind_counter = fun counter -&gt; (counter.create) }
*)


--- Lists of composable functions.

module Funlist : sig

(* The funlist datatype *)
type ('a, 'b) t

(* Constructors *)
val nil : ('a, 'a) t
val cons : ('a -&gt; 'b) -&gt; ('b, 'c) t -&gt; ('a, 'c) t

(* Applying a value to a composition *)
val apply : ('a, 'b) t -&gt; 'a -&gt; 'b

end = struct

(* The type expressed by the four types below is :
   type ('a, 'b) t = Nil of ('a -&gt; 'b)
                   | Cons of exists 'c. ('a -&gt; 'c) * ('c, 'b) t  *)

type ('a, 'b) t = Nil of ('a -&gt; 'b) | Cons of ('a, 'b) packed_list
and ('a, 'b, 'c) list_data = ('a -&gt; 'c) * ('c, 'b) t
and ('a, 'b, 'z) list_scope = { bind_list : 'c. ('a, 'b, 'c) list_data
-&gt; 'z}
and ('a, 'b) packed_list = { open_list : 'z. ('a, 'b, 'z) list_scope -&gt;
'z }

(* Packing and unpacking lists *)
let pack_list h t = { open_list = fun scope -&gt; scope.bind_list (h,t) }
let with_packed_list p e = p.open_list e

(* Constructors *)
let nil = Nil(fun x -&gt; x)
let cons h t = Cons(pack_list h t)


(* The following type is introduced to avoid the problem of polymorphic
recursion that comes while attempting a naive implementation of the
apply
funtion. The idea was taken from Laufer, Odersky, Polymorphic Type
Inference
and Abstract Data Types, 1994. *)

(* The type expressed by the three types below is :
    type 'b packed_apply = exists 'a. ('a, 'b) t * 'a
*)
type ('a, 'b) apply_data = ('a, 'b) t * 'a
type ('b, 'z) apply_scope = { bind_apply : 'a. ('a, 'b) apply_data -&gt;
'z}
type 'b packed_apply = { open_apply : 'z. ('b, 'z) apply_scope -&gt; 'z}

(* Packing and unpacking applications *)
let pack_apply l x = { open_apply = fun scope -&gt; scope.bind_apply (l,x)
}
let with_packed_apply p e = p.open_apply e

let rec apply' a =
  with_packed_apply
    a { bind_apply = function
      | Nil id, x -&gt; id x
      | Cons l, x -&gt;
          with_packed_list
            l { bind_list = function h,t -&gt; apply' (pack_apply t (h x))}}

let apply l x = apply' (pack_apply l x)

end

(* Example of use *)
let twice x = 2*x
let double x = (x, x)
let list = Funlist.cons twice (Funlist.cons (( = ) 4) (Funlist.cons
double Funlist.nil))
let a, b = Funlist.apply list 2
    </pre><i>Jean-Christophe Filliatre answered:</i><br /><pre>
&gt; I usually need three types to encode an existential type. Does anybody
&gt; see a simpler way of doing that ?

I guess  you really need  these three types  (at least before  we have
rank-2 polymorphism  in ocaml). As a  attempt to justify  this, let us
try to make a more generic, functorized, code from your first example.
It takes  the first of your  three types as argument  ('a counter) and
returns the third one (packed_counter) as a result (so obviously these
two types you need).

The functor looks like

        module Make(X : sig type 'a t end) : sig
          type t (* the existential type exists 'a. 'a t *)
          val pack : 'a X.t -&gt; t
          ...
        end = struct
          ...
        end

To be  able to use  the abstract  type t, we  would like to  provide a
function similar to your with_packed_counter, of type

        val use : ('a. 'a X.t -&gt; 'b) -&gt; t -&gt; 'b

Since we do not have  rank-2 polymorphism, your solution introduces an
intermediate record type, as the type of the first argument.

Finally we get this:

        module Make(X : sig type 'a t end) : sig
          type t
          val pack : 'a X.t -&gt; t 
          type 'a user = { f : 'b. 'b X.t -&gt; 'a }
          val use : 'a user -&gt; t -&gt; 'a
        end = struct
          type 'a user = { f : 'b. 'b X.t -&gt; 'a }
          type t = { pack : 'a. 'a user -&gt; 'a }
          let pack impl = { pack = fun user -&gt; user.f impl }
          let use f p = p.pack f
        end

and it seems  that we can't avoid the three  types solution. Note that
for other examples requiring the returned type to be polymorphic (e.g. 
the compositional list example) you need to write another functor.
    </pre><i>Daniel Bünzli also said:</i><br /><pre>
Thanks for your feedback.

Besides, I would just like to point out that the way I solved the
problem of polymorphic recursion for the apply function in the example
of the list of composable function cannot be applied in general.

A general way to solve the problem of polymorphic recursion was given
by Brian Rogoff in a previous post [1]. Applying this solution gives
the following (much simpler and maybe more efficient) code.

Daniel

[1] <a href="http://caml.inria.fr/archives/200208/msg00334.html">http://caml.inria.fr/archives/200208/msg00334.html</a>

module Funlist : sig

(* The funlist datatype *)
type ('a, 'b) t

(* Constructors *)  
val nil : ('a, 'a) t
val cons : ('a -&gt; 'b) -&gt; ('b, 'c) t -&gt; ('a, 'c) t

(* Applying a value to a composition *)
val apply : ('a, 'b) t -&gt; 'a -&gt; 'b

end = struct 
(* List of composable functions.

   The intended type expressed by the four types below is :
   type ('a, 'b) t = Nil of ('a -&gt; 'b)
                   | Cons of exists 'c. ('a -&gt; 'c) * ('c, 'b) t
*)
type ('a, 'b) t = Nil of ('a -&gt; 'b) | Cons of ('a, 'b) packed_list
and ('a, 'b, 'z) list_scope = { bind_list : 'c. ('a -&gt; 'c) * ('c, 'b) t
-&gt; 'z}
and ('a, 'b) packed_list = { open_list : 'z. ('a, 'b, 'z) list_scope -&gt;
'z }

(* Packing and unpacking lists *)
let pack_list h t = { open_list = fun scope -&gt; scope.bind_list (h,t) }
let with_packed_list p e = p.open_list e

(* Constructors *)
let nil = Nil(fun x -&gt; x)
let cons h t = Cons(pack_list h t)

(* Type to handle the polymorphic recursion of the apply function *)
type poly_rec = { apply : 'a 'b. poly_rec -&gt; ('a, 'b) t -&gt; 'a -&gt; 'b }
let apply' r l x = match l with
| Nil id -&gt; id x
| Cons l -&gt;
    with_packed_list l { bind_list = function h,t -&gt; r.apply r t (h x) }

let poly_rec = { apply = apply' }
let apply l x = apply' poly_rec l x

end

(* Example of use *)
let twice x = 2*x
let double x = (x, x)
let list = Funlist.cons twice (Funlist.cons (( = ) 4) (Funlist.cons
double Funlist.nil))
let a, b = Funlist.apply list 2
    </pre><i>Brian Rogoff answered:</i><br /><pre>
I blame Jacques Garrigue for that trick! He is entitled to transfer the blame
to whomever he chooses. ;-)

You may also want to consider using the recursive module feature introduced in
OCaml 3.07 for this too. Unfortunately, you can't (yet, Xavier says he's
working on it)  apply it directly to Funlist on account of your nil. That would
be best, but by wrapping apply I think you can do it like so:

module Funlist : sig

(* The funlist datatype *)
  type ('a, 'b) t

(* Constructors *)
  val nil : ('a, 'a) t
  val cons : ('a -&gt; 'b) -&gt; ('b, 'c) t -&gt; ('a, 'c) t

 (* Applying a value to a composition *)
  val apply : ('a, 'b) t -&gt; 'a -&gt; 'b

 end = struct 
(* List of composable functions.

     The intended type expressed by the four types below is :
     type ('a, 'b) t = Nil of ('a -&gt; 'b)
                     | Cons of exists 'c. ('a -&gt; 'c) * ('c, 'b) t
 *) 
   type ('a, 'b) t = Nil of ('a -&gt; 'b) | Cons of ('a, 'b) packed_list
   and ('a, 'b, 'z) list_scope = { bind_list : 'c. ('a -&gt; 'c) * ('c, 'b) t
                                   -&gt; 'z}
   and ('a, 'b) packed_list = { open_list : 'z. ('a, 'b, 'z) list_scope -&gt;
     'z }

         (* Packing and unpacking lists *)
   let pack_list h t = { open_list = fun scope -&gt; scope.bind_list (h,t) }
   let with_packed_list p e = p.open_list e

 (* Constructors *)
   let nil = Nil(fun x -&gt; x)   
   let cons h t = Cons(pack_list h t)

   module rec PolyRec : sig val apply : ('a, 'b) t -&gt; 'a -&gt; 'b end =
     struct
       let apply l x =
         match l with
         | Nil id -&gt; id x
         | Cons l -&gt;
             with_packed_list
           l { bind_list = function h,t -&gt; PolyRec.apply t (h x) }
     end
   let apply = PolyRec.apply

 end
    </pre><a name="2" /><hr /><h3>Adding an ocaml interpreter to my C program</h3><i>Kip Macy asked and Richard Jones answered:</i><br /><pre>
&gt; It is possible to add many scripting languages as configuration
&gt; languages for an arbitrary C program. One can transfer control
&gt; to the interpreter, let it use the C bindings to modify program  
&gt; state, and then get control back. I'll give perl as an example:
[...]
&gt; I don't see any obvious way of doing this with ocaml.

It's documented here (right at the bottom, in the "Advanced" section):

<a href="http://caml.inria.fr/ocaml/htmlman/manual032.html">http://caml.inria.fr/ocaml/htmlman/manual032.html</a>

Basically, you call caml_main, which runs your initialization code
(written in OCaml), which registers C callbacks, which your C can then
execute by calling callbackN ().

What the manual doesn't tell you is that none of this stuff works at
all if your C code is contained in a dynamically linked library (.so
file), unless you undertake some pretty horrific hacks.  Particularly
if you plan to use the OCaml Dynlink module at the same time.  See the
code in my mod_caml project[1] for details.

Also, it only works for bytecode.  (And it only works for compiled
code - linking in the toplevel so you can parse ML directly is a
different thing entirely).

&gt; I'm using perl as a powerful macro language for gdb by just doing:
&gt; (gdb) sourceperl mycoreanalyser.pl
&gt; &lt;OUTPUT&gt;

You might also be interested in [2].

Rich.

[1] <a href="http://www.merjis.com/developers/mod_caml/">http://www.merjis.com/developers/mod_caml/</a>
[2] <a href="http://www.merjis.com/developers/perl4caml/">http://www.merjis.com/developers/perl4caml/</a>
    </pre><i>Damien Doligez answered another question about this:</i><br /><pre>
&gt;Actually, perhaps I didn't describe it too well, but the OCaml case is
&gt;probably quite a bit simpler than Perl.  What would be really nice for
&gt;OCaml would be some way to have all the identifiers properly prefixed,
&gt;eg. with caml_ and CAML_.  Even if was an optional
&gt;-DCAML_CLEAN_SYMBOLS or something, that would be a huge help.     

Just check out the CVS version.  I spent some of my Christmas holidays
implementing exactly that.

Every global symbol exported by the runtime system now starts with
"caml_", with the only exception of "main", and every global symbol
produced by ocamlopt now starts with "caml".

The change is transparent: any C source that interfaces with OCaml
still works as before, thanks to a bunch of #defines in a new
header file (compatibility.h), which is included from the other
caml header files.  In your code that uses the new names directly,
you can #define CAML_NAME_SPACE before including the header files,  
and then compatibility.h is not included.

I may have broken some of the native-code ports in the process,
so if you have one of the more exotic architectures, please try
the CVS version and report any problems you find.  It was tested
on PPC/MacOSX, i386/Linux, alpha/Tru64Unix, and IA64 (I don't know
which OS).

We still need to do it to the pieces of C code in otherlibs.

We haven't done it for the macros, because name space pollution
is much less problematic at that level.
    </pre><hr /><h3>Using folding to read the cwn in vim 6+</h3><p>Here is a quick trick to help you read this CWN if you are viewing it using vim (version 6 or greater).</p><pre>:set foldmethod=expr<br />:set foldexpr=getline(v:lnum)=~'^=\\{78}$'?'&amp;lt;1':1<br />zM</pre><p>If you know of a better way, please let me know.</p><hr /><h3>Old cwn</h3><p>If you happen to miss a CWN, you can <a href="mailto:alan.schmitt@polytechnique.org">send me a message</a> and I'll mail it to you, or go take a look at <a href="http://alan.petitepomme.net/cwn/">the archive</a> or the <a href="http://alan.petitepomme.net/cwn/cwn.rss">RSS feed of the archives</a>.</p><p>If you also wish to receive it every week by mail, you may subscribe <a href="http://lists.idyll.org/listinfo/caml-news-weekly/">online</a>.</p><hr /><p align="right">
      <i>
        <a href="http://alan.petitepomme.net/">Alan Schmitt</a>
      </i>
    </p><p align="right">
      <script type="text/javascript">var sc_project=1252199; 
                 var sc_invisible=0; 
                 var sc_partition=11; 
                 var sc_security="e7c40dd3"; 
                 var sc_text=3;</script>
      <script type="text/javascript" src="http://www.statcounter.com/counter/counter_xhtml.js" />
      <script src="http://www.google-analytics.com/urchin.js" type="text/javascript" />
      <script type="text/javascript">_uacct = "UA-2289702-1";
								urchinTracker();</script>
    </p></body>
</html>